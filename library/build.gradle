apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

ext {
    versionName = "1.1.0"
}

group = 'com.xfinity'
version = project.versionName

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 2
        versionName project.versionName
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        project.archivesBaseName = "loadingdots-$versionName"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'org.jetbrains:annotations-java5:15.0'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'androidx.appcompat:appcompat:1.0.0'
    testCompile 'junit:junit:4.12'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    destinationDir = file("../javadoc/")
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

//Creating sources with comments
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocJar
    archives androidSourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name "Loading Dots Library"
                packaging 'aar'
                description 'A LoadingDots view based on AnimatedVectorDrawable. Backwards compatible to API 11'
                artifactId "loadingdots"
                url 'https://github.com/Comcast/loadingdots'

                scm {
                    url 'scm:git@github.com:Comcast/loadingdots.git'
                    connection 'scm:git@github.com:Comcast/loadingdots.git'
                    developerConnection 'scm:git@github.com:Comcast/loadingdots.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'dapp'
                        name 'Mark Dappollone'
                    }
                }
            }
        }
    }
}